plugins {
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.dokka'
    id 'io.gitlab.arturbosch.detekt'
    id 'de.marcphilipp.nexus-publish'
}

apply from: "$rootProject.projectDir/multiplatform.gradle"

task compileTemplates(type: TemplateTask) {
    inputDir = file('src/template')
    outputDir = file("$buildDir/generated/template")
}

task compileTestTemplates(type: TemplateTask) {
    inputDir = file('src/testTemplate')
    outputDir = file("$buildDir/generated/testTemplate")
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation "com.willowtreeapps.opentest4k:opentest4k:$opentest4k_version"
            }
            kotlin.srcDirs += files(compileTemplates)
        }
        commonTest {
            kotlin.srcDirs += files(compileTestTemplates)
        }
        jvmMain {
            dependencies {
                compileOnly kotlin('reflect')
            }
        }
        jvmTest {
            dependencies {
                implementation project(':java-interop')
            }
        }
    }
}

[compileKotlinJvm, compileKotlinJsLegacy, compileKotlinJsIr].each { it.dependsOn(compileTemplates) }
[compileTestKotlinJvm, compileTestKotlinJsLegacy, compileTestKotlinJsIr].each { it.dependsOn(compileTestTemplates) }