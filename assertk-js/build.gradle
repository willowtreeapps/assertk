apply plugin: 'kotlin-platform-js'
apply plugin: 'java-library'
apply plugin: 'com.moowork.node'

buildscript {
    dependencies {
        classpath "com.eriwen:gradle-js-plugin:2.14.1"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.3.1'
    }
}

dependencies {
    expectedBy project(":assertk-common")
    api "org.jetbrains.kotlin:kotlin-stdlib-js"
    // node_modules does not populate this dep if this is api or implementation
    compile 'com.willowtreeapps.opentest4k:opentest4k-js:1.1.0'
    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}


[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "umd"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
}


task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

ext {
    nodeVersion = '8.11.4'
}

node {
    version = nodeVersion
    download = true
}

npmInstall.workingDir = file(projectDir)

task runNodeTests(type: NodeTask, dependsOn: [compileTestKotlin2Js, npmInstall, populateNodeModules]) {
    script = file("$projectDir/node_modules/.bin/mocha")
    args = [projectDir.toPath().relativize(file(compileTestKotlin2Js.outputFile).toPath())]
}

test.dependsOn runNodeTests

apply from: '../publish.gradle'
